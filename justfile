# ============ Shell configuration for Windows ============

# On Windows the "bash" shell from Git for Windows is used.
# If Git is installed in a non-standard location, edit the path below.
set windows-shell := ["C:/Program Files/Git/bin/bash", "-cu"]

# ============ Variables used in recipes ============

# Detect WSL2 variable
_wsl2_check := `[ -n "${WSL_INTEROP:-}" ] && [ -z "${JUST_TEMPDIR:-}" ] && echo "ERROR" || echo "OK"`

# Load environment variables from config.public.mk or specified file
set dotenv-load := true
# set dotenv-filename := env_var_or_default("LINKML_ENVIRONMENT_FILENAME", "config.public.mk")
set dotenv-filename := x'${LINKML_ENVIRONMENT_FILENAME:-config.public.mk}'

# Set shebang line for cross-platform Python recipes (assumes presence of launcher on Windows)
shebang := if os() == 'windows' {
  'py'
} else {
  '/usr/bin/env python3'
}

# Environment variables with defaults
schema_name := env_var_or_default("LINKML_SCHEMA_NAME", "_no_schema_given_")
source_schema_dir := env_var_or_default("LINKML_SCHEMA_SOURCE_DIR", "")
config_yaml := if env_var_or_default("LINKML_GENERATORS_CONFIG_YAML", "") != "" {
  "--config-file " + env_var_or_default("LINKML_GENERATORS_CONFIG_YAML", "")
} else {
  ""
}
gen_doc_args := env_var_or_default("LINKML_GENERATORS_DOC_ARGS", "")
gen_java_args := env_var_or_default("LINKML_GENERATORS_JAVA_ARGS", "")
gen_owl_args := env_var_or_default("LINKML_GENERATORS_OWL_ARGS", "")
gen_pydantic_args := env_var_or_default("LINKML_GENERATORS_PYDANTIC_ARGS", "")
gen_ts_args := env_var_or_default("LINKML_GENERATORS_TYPESCRIPT_ARGS", "")

# Import map: rewrites canonical URI imports (e.g. `dpvs:schema/dpvs_core`) to
# local files during build. Downstream consumers do NOT need this file; they
# resolve the same URIs over HTTP via the w3id.org redirect.
#
# The committed `importmap.json` uses *relative* values for portability and
# IDE/linter consumption. At build time we materialise `tmp/importmap.json`
# with *absolute* paths because `linkml.utils.schemaloader.SchemaLoader`
# joins importmap values with the importing schema's directory (which would
# otherwise produce a doubled `src/dpvs/schema/src/dpvs/...` path).
import_map_path := env_var_or_default("LINKML_IMPORT_MAP", "tmp/importmap.json")
import_map := if import_map_path != "" {
  "--importmap " + import_map_path
} else {
  ""
}

# Directory variables
src := "src"
dest := "project"
pymodel := src / schema_name / "datamodel"
source_schema_path := source_schema_dir / schema_name + ".yaml"
# Pre-merged, self-contained schema used as input to `gen-project`. See
# `_merged-schema` recipe and `scripts/merge_schema.py` for the rationale: the
# SchemaLoader-based sub-generators inside gen-project (python/sqltable/excel)
# leak unresolved URI imports into a secondary SchemaView that ignores the
# import map, so we must feed them a schema whose `imports:` list is empty.
merged_schema_path := "tmp" / schema_name + ".yaml"
docdir := "docs/elements"  # Directory for generated documentation
distrib_schema_path := "docs/schema"  # Directory for publishing schema artifacts

# ============== Project recipes ==============

# List all commands as default command. The prefix "_" hides the command.
_default: _status
    @{{ if _wsl2_check == "ERROR" { "echo 'WSL2 detected: run export JUST_TEMPDIR=/tmp'" } else { "" } }}
    @just --list

# WSL2 status check - warns but does not abort (safe to use in _status/_default)
[private]
_wsl2_status_check:
    @if [ -n "${WSL_INTEROP:-}" ] && [ -z "${JUST_TEMPDIR:-}" ]; then \
      echo "WARNING: WSL2 detected but JUST_TEMPDIR is not set."; \
      echo "Shebang recipes will fail with 'Permission denied' errors."; \
      echo "Fix: run 'export JUST_TEMPDIR=/tmp'"; \
    fi

# WSL2 compatibility check - fails early with helpful message
[private]
_wsl2_compat_check:
    @if [ -n "${WSL_INTEROP:-}" ] && [ -z "${JUST_TEMPDIR:-}" ]; then \
      echo "ERROR: WSL2 detected but JUST_TEMPDIR is not set."; \
      echo "Shebang recipes will fail with 'Permission denied' errors."; \
      echo ""; \
      echo "Fix: run this command:"; \
      echo ""; \
      echo "  export JUST_TEMPDIR=/tmp"; \
      echo ""; \
      echo "Or add it to your ~/.bashrc for persistence."; \
      exit 1; \
    fi

# Initialize a new project (use this for projects not yet under version control)
[group('project management')]
setup: _wsl2_compat_check _check-config _git-init install _git-add && _setup_part2
  git commit -m "Initialise git with minimal project" -a || true

_setup_part2: gen-project gen-doc
  @echo
  @echo '=== Setup completed! ==='
  @echo 'Various model representations have been created under directory "project". By default'
  @echo 'they are ignored by git. You decide whether you want to add them to git tracking or'
  @echo 'continue to git-ignore them as they can be regenerated if needed.'
  @echo 'For tracking specific subfolders, add !project/[foldername]/* line(s) to ".gitignore".'

# Install project dependencies
[group('project management')]
install:
  uv sync --group dev

# Updates project template and LinkML package
[group('project management')]
update: _update-template _update-linkml

# Clean all generated files
[group('project management')]
clean: _wsl2_compat_check _clean_project
  rm -rf tmp
  rm -rf {{docdir}}/*.md

# (Re-)Generate project and documentation locally
[group('model development')]
site: gen-project gen-doc

# Deploy documentation site to Github Pages
[group('deployment')]
deploy: site
  mkd-gh-deploy

# Run all tests
[group('model development')]
test: _test-schema _test-python _test-examples

# Run linting
[group('model development')]
lint:
  uv run linkml-lint {{source_schema_dir}}

# Generate md documentation for the schema and add artifacts.
#
# Workaround for upstream LinkML bug (see ISSUE.md §9):
# `DocGenerator.__post_init__` re-creates its `SchemaView` and drops
# `importmap`/`base_dir`, causing URI-style imports like
# `dpvs:schema/dpvs_core` to be fetched over HTTP (404 against
# raw.githubusercontent.com). We sidestep this by feeding `gen-doc`
# the pre-merged, self-contained schema produced for `gen-project`
# (`tmp/dpvs.yaml`), which has no `imports:` left to mis-resolve.
[group('model development')]
gen-doc: _merged-schema && _add-artifacts
  uv run gen-doc {{import_map}} {{gen_doc_args}} -d {{docdir}} {{merged_schema_path}}

# Build docs and run test server
[group('model development')]
testdoc: gen-doc _serve

# Generate the Python data models (dataclasses & pydantic)
gen-python: _importmap _merged-schema
  uv run gen-project -d  {{pymodel}} -I python {{merged_schema_path}}
  uv run gen-pydantic {{import_map}} {{gen_pydantic_args}} {{source_schema_path}} > {{pymodel}}/{{schema_name}}_pydantic.py

# Generate project files including Python data model
# Build order: gen-linkml (build schema from upstream) -> apply-sssom-overlay
# (merge curated mappings) -> gen-project (downstream artifacts).
[group('model development')]
gen-project: _merged-schema
  uv run gen-project {{config_yaml}} -d {{dest}} {{merged_schema_path}}
  mkdir -p {{pymodel}}
  mv {{dest}}/*.py {{pymodel}}/
  uv run gen-pydantic {{import_map}} {{gen_pydantic_args}} {{source_schema_path}} > {{pymodel}}/{{schema_name}}_pydantic.py

  @# Some generators ignore config_yaml or cannot create directories, so we run them separately.
  uv run gen-java {{import_map}} {{gen_java_args}} --output-directory {{dest}}/java/ {{source_schema_path}}

  @if [ ! -d "{{dest}}/typescript" ]; then \
    mkdir -p {{dest}}/typescript ; \
  fi
  uv run gen-typescript {{import_map}} {{gen_ts_args}} {{source_schema_path}} > {{dest}}/typescript/{{schema_name}}.ts

  @if [ ! -d "{{dest}}/owl" ]; then \
    mkdir -p {{dest}}/owl ; \
  fi
  uv run gen-owl {{import_map}} {{gen_owl_args}} {{source_schema_path}} > "{{dest}}/owl/{{schema_name}}.owl.ttl"

# ============== Migrations recipes for Copier ==============

# Hidden command to adjust the directory layout on upgrading a project
# created with linkml-project-copier v0.1.x to v0.2.0 or newer.
# Use with care! - It may not work for customized projects.
_post_upgrade_v020: _wsl2_compat_check && _post_upgrade_v020py
  mv docs/*.md docs/elements

_post_upgrade_v020py:
    #!{{shebang}}
    import subprocess
    from pathlib import Path
    # Git move files from folder src to folder dest
    tasks = [
        (Path("src/docs/files"), Path("docs")),
        (Path("src/docs/templates"), Path("docs/templates-linkml")),
        (Path("src/data/examples"), Path("tests/data/")),
    ]
    for src, dest in tasks:
        for path_obj in src.rglob("*"):
            if not path_obj.is_file():
                continue
            file_dest = dest / path_obj.relative_to(src)
            if not file_dest.parent.exists():
                file_dest.parent.mkdir(parents=True)
            print(f"Moving {path_obj} --> {file_dest}")
            subprocess.run(["git", "mv", str(path_obj), str(file_dest)])
    print(
        "Migration to v0.2.x completed! Check the changes carefully before committing."
    )

# ============== Hidden internal recipes ==============

# Materialise an absolute-path import map at `tmp/importmap.json`.
# Source of truth is the committed `importmap.json` (relative paths). At
# build time we rewrite its string values to absolute paths because
# `SchemaLoader` joins importmap values with the importing schema's
# directory; absolute values short-circuit that join.
_importmap:
    #!{{shebang}}
    import json
    import os
    from pathlib import Path
    repo_root = Path.cwd().resolve()
    src_path = repo_root / "importmap.json"
    if not src_path.exists():
        raise SystemExit(f"Missing {src_path}")
    data = json.loads(src_path.read_text())
    abs_data = {
        k: (str(repo_root / v) + os.sep if isinstance(v, str) and not os.path.isabs(v) else v)
        for k, v in data.items()
    }
    dest = repo_root / "tmp" / "importmap.json"
    dest.parent.mkdir(parents=True, exist_ok=True)
    dest.write_text(json.dumps(abs_data, indent=2) + "\n")
    print(f"Wrote {dest}")

# Materialise a self-contained merged schema at `{{merged_schema_path}}`.
# Required because `gen-project`'s SchemaLoader-based sub-generators
# (python/sqltable/excel) construct a secondary SchemaView from the merged
# schema *without* propagating `--importmap`, so any unresolved URI imports
# in the merged schema's `imports:` list get HTTP-fetched and fail.
# `scripts/merge_schema.py` resolves imports up-front and strips them.
# Depends on `apply-sssom-overlay` so the source schemas exist (and carry
# the curated mappings) before we try to flatten them.
_merged-schema: _importmap apply-sssom-overlay
  uv run python scripts/merge_schema.py {{source_schema_path}} \
    --importmap {{import_map_path}} \
    --output {{merged_schema_path}}

# Show current project status
_status: _wsl2_status_check _check-config
  @echo "Project: {{schema_name}}"
  @echo "Source: {{source_schema_path}}"

# Check project configuration
_check-config:
    #!{{shebang}}
    import os
    schema_name = os.getenv('LINKML_SCHEMA_NAME')
    if not schema_name:
        print('**Project not configured**:\n - See \'.env.public\'')
        exit(1)
    print('Project-status: Ok')

# Update project template
_update-template:
  copier update --trust --skip-answered

# Update LinkML runtime and LinkML to latest versions
_update-linkml:
  uv lock --upgrade-package linkml-runtime --upgrade-package linkml

# Test schema generation
#
# Use the pre-merged, self-contained schema (`tmp/dpvs.yaml`) for the same
# reason as `gen-doc` / `gen-project`: SchemaLoader-based sub-generators
# inside `gen-project` drop `--importmap` and try to fetch URI imports
# over HTTP (see ISSUE.md §8/§9). Feeding the merged YAML sidesteps it.
_test-schema: _merged-schema
  uv run gen-project {{config_yaml}} -d tmp {{merged_schema_path}}

# Run Python unit tests with pytest
_test-python: gen-python
  uv run python -m pytest

# Run example tests
#
# NOTE: We deliberately do NOT use the stock `linkml-run-examples` CLI here.
# That CLI hardcodes `JsonschemaValidationPlugin(closed=True)`, which rejects
# every domain-less DPV slot (`has_location`, `is_subsidiary_of`, ...) on
# every concrete class -- even though the dpvs schema deliberately attaches
# those slots to `DpvThing` rather than to a specific class (open-world).
# `scripts/run_examples_open_world.py` is a drop-in wrapper that uses the
# same `Validator` machinery in open-world mode, matching `tests/test_data.py`
# and the documented design in `docs/about.md`. See `ISSUE.md` for the
# upstream gap.
_test-examples: _ensure_examples_output _merged-schema
  uv run python scripts/run_examples_open_world.py \
    --schema {{merged_schema_path}} \
    --input-directory tests/data/dpvcg/valid \
    --counter-example-input-directory tests/data/dpvcg/invalid \
    --output-directory examples/output \
    > examples/output/README.md

# Add the merged model to docs/schema.
_gen-yaml: _importmap
  -mkdir -p {{distrib_schema_path}}
  uv run gen-yaml {{import_map}} {{source_schema_path}} > {{distrib_schema_path}}/{{schema_name}}.yaml

# Overridable recipe to add project-specific artifacts to the distribution schema path
_add-artifacts:

# Run documentation server
_serve:
  uv run mkdocs serve

# Initialize git repository
_git-init:
  git init

# Add files to git
_git-add:
  git add .

# Commit files to git
_git-commit:
  git commit -m 'chore: just setup was run' -a

# Show git status
_git-status:
  git status

_clean_project:
    #!{{shebang}}
    import shutil, pathlib
    # remove the generated project files
    for d in pathlib.Path("{{dest}}").iterdir():
        if d.is_dir():
            print(f'removing "{d}"')
            shutil.rmtree(d, ignore_errors=True)
    # remove the generated python data model
    for d in pathlib.Path("{{pymodel}}").iterdir():
        if d.name == "__init__.py":
            continue
        print(f'removing "{d}"')
        if d.is_dir():
            shutil.rmtree(d, ignore_errors=True)
        else:
            d.unlink()
    # remove the generated LinkML schema files (regenerated by `gen-linkml`
    # from upstream-releases/dpv/). The top-level `dpvs.yaml`, the
    # aggregate `dpvs_core.yaml`, and every per-module schema under
    # `modules/` are all derived artifacts; the mappings overlay
    # (`apply-sssom-overlay`) is then re-applied on top during the build.
    schema_dir = pathlib.Path("{{source_schema_dir}}")
    for name in ("{{schema_name}}.yaml", "{{schema_name}}_core.yaml"):
        f = schema_dir / name
        if f.exists():
            print(f'removing "{f}"')
            f.unlink()
    modules_dir = schema_dir / "modules"
    if modules_dir.is_dir():
        for f in sorted(modules_dir.glob("*.yaml")):
            print(f'removing "{f}"')
            f.unlink()
    # remove the merged/self-contained schema used as gen-project input
    merged = pathlib.Path("{{merged_schema_path}}")
    if merged.exists():
        print(f'removing "{merged}"')
        merged.unlink()

_ensure_examples_output:  # Ensure a clean examples/output directory exists
  -mkdir -p examples/output
  -rm -rf examples/output/*.*

# ============== Include project-specific recipes ==============

import "project.justfile"

# ====== Override recipes from above with custom versions =======

# Uncomment the following line to allow duplicate recipe names
#set allow-duplicate-recipes

# Overriding recipes from the root justfile by adding a recipe with the same
# name in an imported file is not possible until a known issue in just is fixed,
# https://github.com/casey/just/issues/2540 - So we need to override them here.
